# derived from TFC layer
#
# Class things:
var name = 'GROUNDSERVICES-vehicle';
var parents = [SymbolLayer.Controller];
var __self__ = caller(0)[0];
SymbolLayer.Controller.add(name, __self__);
SymbolLayer.add(name, {
	parents: [MultiSymbolLayer],
	type: name, # Symbol type
	df_controller: __self__, # controller to use by default -- this one
});

var model_root = props.globals.getNode("/ai/models/");

var new = func(layer) {
	var m = {
		parents: [__self__],
		layer: layer,
		map: layer.map,
		listeners: [],		
	};
	layer.searcher._equals = func(l,r) l.equals(r);
	m.addVisibilityListener();

	return m;
};

var del = func() {
	foreach (var l; me.listeners)
		removelistener(l);
};

var MapElement = {
	new: func(id, posnode, type, categorie, layer=nil) {
		var m = {
			# Note: because this inherits from props.Node, Symbol.Controller.equals
			# will call l.equals(r) -- the one defined below
			parents: [MapElement, geo.Coord, posnode], # note we don't implement a full geo.Coord API
			id: id,
			node: posnode,
			pos: posnode,
			type: type,
			categorie: categorie,
		};
		#groundservices.logging.info("new MapElement: id="~m.id~", type="~m.type~",categorie="~m.categorie);
		return m;
	},

	equals: func(other) other.id == me.id and other.categorie == me.categorie,

	latlon: func() { # this makes sure to look like a geo.Coord to MapStructure, but will internally use the AI/MP traffic properties instead
	    #logging.debug("latlonalt:"~me.pos.getValue("latitude-deg")~" "~me.pos.getValue("longitude-deg")~" " ~me.pos.getValue("altitude-ft"));
		return [
			me.pos.getValue("latitude-deg"),
			me.pos.getValue("longitude-deg"),
			me.pos.getValue("altitude-ft")
		];
	},
	getCategorie: func() {
	    return me.categorie;
	},
};

var searchCmd = func {
	#groundservices.logging.debug( "Doing query: "~name);
	
	var result = [];
	var models = 0;

	foreach (var t; model_root.getChildren("gsvehicle")) {
		if (!t.getValue("valid"))
		    continue;
		var t_id = t.getNode("id");
		if (t_id == nil or t_id.getValue() == -1)
		    continue;
		models += 1;
		var (lat,lon) = (t.getValue("position/latitude-deg"),
		                 t.getValue("position/longitude-deg"));
		if (lat == nil or lon == nil) {
			logging.warn("alert", "lat/lon was nil for AI node "~t.getPath());
			continue;
		}
		var vehicle = groundservices.findVehicleById(t.getValue("id"));
		var categorie = 'i';
		var type = t.getValue("type");
		if (type == groundservices.VEHICLE_CAR) {
            if (!vehicle.gsc.isIdle()) {
                categorie = 'b';
            }
		}
		if (type == groundservices.VEHICLE_AIRCRAFT) {
            categorie = 'A';
        }
        #groundservices.logging.debug("found vehicle. type="~type~",categorie="~categorie);
		append(result, MapElement.new(t.getValue("id"),t.getNode("position",1),type, categorie, me.layer));
	}
	# sp id might be equal to vehicle id, so add 100000
	foreach (var t; groundservices.servicepointsN.getChildren("servicepoint")) {
        #groundservices.logging.info("map servicepoint " ~ t.getValue("id"));
        append(result, MapElement.new(t.getValue("id")+100000, t.getNode("position",1),"-", "s", me.layer));
    }
	# main aircraft
    append(result, MapElement.new(-200, props.globals.getNode("position",1),"-", "a"));
	
	#groundservices.logging.debug("Found "~size(result)~" VehicleModel's in range out of "~models~" total.");
	return result;
};

